# Вы можете расположить сценарий своей игры в этом файле.

# Определение персонажей игры.
define s = Character('Стас', color="#c8ffc8", image = "stas")
define d = Character('Дима', color="#c8ffc8", image = "dimas")
define gp = Character('Гопник', color="#c8ffc8", image = "gopnik")
define v = Character('Валера', color="#c8ffc8", image = "valera")
define st = Character('Сторож', color="#c8ffc8", image = "storozh")
define m = Character('Мама', color="#c8ffc8", image = "mama")
define who = Character('???', color="#c8ffc8")

define sts = Character('Незнакомец1', color="#c8ffc8")
define dms = Character('Незнакомец2', color="#c8ffc8")

define stm = Character ('Станислав', color="#c8ffc8")
define dm = Character ('Дмитрий', color="#c8ffc8")

define var1 = False
define var2 = False
define var3 = False
define fin = True

define audio.mus_bar = "audio/music/bar.mp3"
define audio.mus_danger = "audio/music/danger.mp3"
define audio.mus_dogez = "audio/music/dogez.mp3"
define audio.mus_fight = "audio/music/fight.mp3"
define audio.mus_stas = "audio/music/stas.mp3"
define audio.mus_walk = "audio/music/walk.mp3"
define audio.mus_run = "audio/music/run.mp3"
define audio.sound_club = "audio/sound/club.mp3"
define audio.mus_ble = "audio/music/pizda.mp3"
define audio.mus_go = "audio/music/road_club.mp3"
define audio.mus_sad = "audio/music/sad.mp3"


# Вместо использования оператора image можете просто
# складывать все ваши файлы изображений в папку images.
# Например, сцену bg room можно вызвать файлом "bg room.png",
# а eileen happy — "eileen happy.webp", и тогда они появятся в игре.

# Игра начинается здесь:
label start:
    play music mus_bar
    scene bg bar
    with fade
    dms "Странно… Столько лет прошло, а мало что здесь изменилось."

    sts "А что, должно было?"

    dms "Не знаю… я просто себя всё ещё некомфортно чувствую. {w}Возможно, это просто с непривычки. Давно я здесь не бывал."

    sts "Здесь – это в этой забегаловке-то? Я чего-то не знаю о твоей буйной молодости?"

    dms "Очень смешно. {w}Да нет, в своём родном городе.{w} Не думал, что он покажется мне настолько… тихим, что ли…"

    sts "Ещё бы, ты ж там катаешься по своей Барселоне, я слыхал там по ночам такая жара происходит…"

    dms "Мадрид, Стас. Да и не отдыхать я туда ездил, а работать. {w}Последний тираж разошёлся на удивление неплохо, вот я на радостях и позволил себе отпуск.{w} С матерью повидаться, тебя поискать…"

    sts "А чего меня искать? Я тут, через дорогу работаю…"

    dms "Ааа, ну теперь-то неудивительно что я на тебя наткнулся. {w}Признаться, думал, что поиски тебя куда больше времени займут."

    sts "Ну, Дим, ты всегда везучим был, сколько я тебя помню. {w}Ну ладно, не то чтобы СОВСЕМ всегда…"

    dms "Так, это было потому, что ты меня соблазнил на ту хрень, а так бы моя репутация везунчика была безупречной!"

    "На этом моменте мы с моим старым приятелем рассмеялись."

    "И очень быстро – одновременно, словно по команде – умолкли."

    "Над нами нависла напряжённая тишина."
    stop music fadeout 1

    "..."

    "Словив его взгляд, я понял: Стас думал ровно о том же, о чём и я."

    "Мы пришли к неизбежной части нашей… {w}скажем так, когда-то отложенного на продолжительное время разговора."

    "Мы вспоминали тот день, когда мы приняли решение."

    "Решение, повлиявшее на нашу жизнь куда сильнее, чем подозревали…"

    scene bg black
    with fade

    "Утро." 

    scene bg room
    with fade
    play music mus_walk
    "Прохладная комната, и такая теплая постель. "

    """
    Единственное, что нарушает эту прекрасную атмосферу – будильник. 

    С каждой секундной он начинает еще больше раздражать, ведь звенит он не просто так. 

    Сегодня среда. Отличный день недели, но только не сегодня. 

    Сегодня по Геометрии будут проводить контрольную. 

    Я бы не сказал, что я ничего не понимаю в геометрии и в других предметах, но обычно мои результаты на контрольных не очень высоки, сколько ты ни старайся. 

    Да и Стас вчера совсем был не в духе, всё приговаривал что по контрольной нихрена не подготовился, и что он просто напросто не появлялся бы, если бы было с кем. 

    Зная Стаса, его жалобы можно перевести как вопрос: “Поддержишь ли ты меня в эту трудную минуту?”. 

    Поэтому мной и моим другом Стасяном было решено прогулять сегодня школу. Я крайне редко прогуливаю. 

    В последний раз такое было несколько лет назад, и то случайно. Однако сегодня все иначе. 

    Решение было принято практически сразу. Поэтому… я хорошенько подготовился к предстоящей операции вчера вечером.

    Привычными действиями я собрал сумку и направился к выходу. 

    Обычно мой будильник установлен на самое крайнее время, поэтому мне нужно поторопиться, чтобы не вызвать лишнего подозрения со стороны родных.

    Проверив карманы на наличие денег я выдвигаюсь на улицу. 

    Скорее всего меня уже заждался товарищ по сегодняшним приключениям. Я бы удивился, не окажись его раньше обычного.
    """
    scene bg black
    with fade
    stop music fadeout 1
    jump utro

    return

label utro:

    scene bg dvor
    with fade
    show stas norm
    play music mus_stas
    "Как я и думал, Стасян на месте. Его можно узнать сразу по его любимой спортивной куртке, которую он одевает всегда."

    "Даже в школу, где необходимо быть в костюме." 

    "Все учителя постоянно делают замечания насчет этого, но он лишь отмахивается, говоря, что куртка – часть костюма… {w}спортивного{w}, а значит считается."

    "Так, конечно, думает только он, однако все довольно быстро устают спорить и пропускают его так."

    s angry "Димон, ты долго там стоять будешь, пошли уже!"

    d norm "Да ладно тебе, торопиться нам некуда, у нас весь день впереди."

    s happy "Ну да, ты прав. Я как-то об этом не подумал"

    d smile "Хех, ну ты даешь.{w} Ладно, куда двинем?"

    s smushenie "Я без понятия. Прогуливаю постоянно, но обычно делаю это дома. У тебя есть идеи?"

    d wat "А сегодня к тебе не завалимся?"

    s smushenie "Не, у меня все дома." 
    extend angry" Еле как вырвался сюда через тонны ругани и фраз о том, какой я никудышный сынок, раз приношу только двойки и тройки…"

    d norm "Да уж, звучит вообще невкусно…"
    extend think " Ладно, давай тогда пойдем… {w}чёрт, и куда ж бы сходить… {w}мест вроде много, да везде уже бывали не раз."

    s proud "Ну смотри{w}, мы можем на компы сходить,"
    extend happy " наконец-то постреляемся сколько влезет."
    extend norm " Ну или если компы достали, пройдём вон по тем дворам, как раз по дороге подумаем, чем бы ещё заняться."
    s proud "И… {w}чё ещё… {w}ну, можем в магазин за сухариками сходить…"

    d smile "Слушай, а неплохо. Давай пойдем…"
    stop music fadeout 1
    jump choise

    return

label choise:
    play music mus_walk
    scene bg dvor
    menu:
        "Что делаем дальше?"

        "Прогуляемся по дворам" if (var1 == False):
            $var1 = True
            jump garage

        "Пойдем на компы" if (var2 == False):           
            $var2 = True
            jump kompiki    

        "Пошли ко мне" if (var3 == False):
            $var3 = True
            jump hata

        "Ладно, давай уже расходиться" if(var1 and var2 and var3):
              jump final

    return

label garage:

    show stas norm

    d ok "Ладно, давай погуляем пока, все равно больше ничего в голову не лезет."

    s happy "Давай, почему нет. Сам же сказал: времени дохерища, хехе."

    scene bg dvor
    with fade

    """
    Мы решили выбрать необычный для нас и одноклассников путь, чтобы не столкнуться случайно с ними. 

    Пробираясь через незнакомые дворы, мы обсуждаем фильм, который посмотрели недавно.
    
    Никогда ранее не думал, что в боевик может уложиться что-то помимо стрелянины… 
    
    Обсуждение идёт настолько бурно и интересно, что мы не заметили, как вышли в совсем незнакомый район. """

    scene bg garage
    with fade

    "Побродив немного, решили остановиться рядом с гаражами."
    
    "Раньше мы часто заглядывали в такие места, потому что там можно найти много всего интересного."
    
    "Ну и побегать по крышам лишний раз тоже приятно."

    show stas norm

    s norm "Ну что, раз мы тут, давай как в старые добрые?"

    d norm "Ты хочешь устроить очередной забег по крышам? Не надоело?"

    s proud "Ну а чё бы и нет? Надоесть – ни в коем случае! Хочу опять видеть твою расстроенную из-за проигрыша морду!"

    "Стасян раскатисто смеется."

    "Видимо ему действительно нравится побеждать, даже в таких незначительных моментах."

    "Вот бы он эту энергию прикладывал в учебу, а не в гонки по гаражам."

    s proud "Чего задумался?"
    extend happy " Зассал?"

    d blya "Ты думаешь хорошая идея в школьной форме бегать по гаражам?"

    "Стасян с гордостью хлопает себя по спортивной куртке."

    "Сразу видно, что предложил он это соревнование не просто так."

    "Да уж, от таких “костюмов” хотя бы толк какой-то есть…"

    s norm "Не, если ты зассал, то базару ноль, давай двинем отсюда…"

    d norm "Ты шибко не выёживайся тут. Я тебя не раз делал, думаешь меня на понт взять? Или ты забыл? Или ты не знал?"

    s proud "Ну так давай проверим, кто кого…"

    "Ехидно улыбаясь, Стасян бросает сумку на землю и примеряется к ближайшему гаражу, высматривая способы залезть на него."

    "Через пару секунд он с ловкостью кота забирается наверх и жестом зовет меня."

    d ok "Так и быть,"
    extend norm " принимаю твой вызов. Посмотрим, кто кому кабину начешет!"
    stop music fadeout 1
    play music mus_stas
    scene bg black
    with fade

    scene bg garage
    with fade

    "В этом раунде Стасян меня обогнал. Видимо я еще не проснулся до конца, ноги не двигаются с привычной лёгкостью. Или это из-за школьной формы? Это не важно. Я готов к следующему раунду."

    scene bg black
    with fade

    scene bg garage
    with fade

    show stas proud

    "В этот раз победа за мной. Лёгкие горят, я выложился на максимум. Рубашка уже минут десять как липнет к моему телу. Стасян, видя это, предложил немного передохнуть. По нему и не скажешь, но сил он также потратил достаточно."

    "Через пару минут я полностью восстановился, Стас видимо тоже готов к продолжению. В данный момент у нас ничья, обязательно нужно последним забегом решить, кто выйдет победителем."
    
    "Однако у меня есть странное чувство, что лучше уходить поскорее."
    stop music fadeout 1
    play music walk
    d norm "Слушай, может двинем? Мне кажется мы тут уже слишком долго. Мало ли, вдруг нас тут поймают…"

    s angry "Решил оставить наше соревнование как есть? Я отсюда без победы не уйду!"

    menu:
        "Что делать?"

        "Настоять на уходе":
            jump garage_move

        "Остатья на финальный забег":
            jump garage_stay


    return

label kompiki:
    play music mus_stas
    show stas norm
    d smile "Ладно, погнали на компы. {w}Раз залететь к тебе не получилось, попробуем там сыграть."

    s happy "А давай. {w}Давненько я там не был."
    extend smushenie" Только это, денег у меня… ну… {w}маловато."

    d "Норм, я перед выходом специально по карманам пошуршал, денег хватит."

    s norm "А ну, покеж… "
    extend proud "А, ого! Ну ты прям богатей, красава!"
    extend smushenie " Спасибо, что прикрываешь мне жопу…"

    d think "Да ладно тебе, потом отработаешь…"

    s angry "Э{w}, не понял…"

    scene bg dvor
    with fade

    """
    Мы довольно быстро определились с местом, где можно сыграть.

    В нашем районе всего несколько клубов, однако один не работает, в другом совсем старые компы, которые не тянут большую часть игр, в которые мы хотели бы сыграть. 

    Так что остановились на месте с говорящим названием «Компики».

    Клуб находится в одном «Торговом центре» недалеко от нас. 
    """

    scene bg komp
    with fade

    """
    На самом деле не знаю, почему его называют ТЦ, если это просто небольшая пристройка около жилых домов, в котором работяги покупают себе еду и пойло на вечер. 

    Само место непримечательное, однако в нем есть свои особенности. 

    Главной из которых можно назвать местный контингент, состоящий преимущественно из любителей отжимать последнюю мелочь. 

    В обычное время я бы не решился идти туда. 

    Однако пока идет школа, там скорее всего никого не будет.

    Да и Стасян на месте, так что отбиться сможем.

    """
    stop music fadeout 1
    play sound sound_club
    scene bg club
    with fade

    """
    Войдя внутрь, мы оказались удивлены, ведь почти все места были заняты. 

    Видимо не мы одни такие умные. 

    Благо для нас как раз осталось парочка мест. 

    Оплатив их, и определившись с игрой, началась битва. 

    Начали мы с давно устоявшейся традиции: со стрелялок. 

    Стас в первую очередь любил решать вопрос “кто круче” именно с них. 

    Хоть я и не сильно хотел играть пока мы добирались, как только я сел за удобное сиденьице, и начался первый раунд… 

    Игровой азарт перебил все мои тревоги на тот момент. 

    Похоже, мне очень хотелось развеяться от дурных мыслей…

    В самый разгар сражения передо мной появилось препятствие, подозрительно похожее по куртке на Стасяна. 

    Посмотрев на место своего друга, я вижу Стасяна, увлечённого игрой. 

    Тогда кто же стоит рядом со мной…

    Как только я подумал об этом, загадочный гость решил первым заговорить.
    """
    show gopnik dark

    who "Эй, друг, дай-ка я тебе покажу, как этого лоха опустить."

    d norm "Да не, я и сам его размотаю. Плавали, знаем…"

    show gopnik norm

    "Посмотрев на своего собеседника, мои догадки подтвердились. Передо мной стоял типичный любитель отжима. Его намерение очевидно. Пока я думал, как действовать в этой ситуации, я почувствовал на своем плече тяжелую руку и не менее тяжелый взгляд."

    gp norm "Мне?"
    extend happy" Да так, поиграть хотелось, а места все заняты. Дай поиграть минут пять, ок?"

    "Именно такого ответа я и ожидал услышать. Уж очень часто в этом клубе происходит такое. Однако я не знаю, как лучше поступить. Играть со Стасяном тоже хочется, но и по лицу лишний раз не очень приятно получить."
    stop sound fadeout 1
    menu:
        "Что делать?"

        "Уступить место":
            jump komp_agree

        "Отказать":
            jump komp_refuse


    return

label hata:
    play music mus_walk
    scene bg dvor

    show stas norm

    d smile "Давай попробуем залететь ко мне. Сегодня мама идёт на работу, так что квартира будет в нашем распоряжении."

    s happy "Похоже на план!"

    scene bg dvor
    with fade

    "По пути к моему дому, мы планировали, чем будем заниматься."

    "Хороших идей на удивление набралось очень много."

    "Как же всё-таки здорово, что наши мысли так хорошо сходятся…"

    "Только вот объявилась маленькое неудобство."

    "Мысли об очередном фильме или играх резко оборвались, когда я увидел около подъезда машину моей мамы."

    show stas norm
    stop music fadeout 1
    d wat "Смотри! {w} Машина на месте. Возможно она ещё дома…"

    s proud "А ты проверь. {w}Вдруг машина сломалась, и она поехала на марике?"
    play music mus_go

    d think "Хм… {w}Такое возможно, правда маловероятно…"

    d smile "Ах да, Она недавно жаловалась, что с ней что-то не так."

    s norm "Ну и вот. Так что давай, проверь."

    d norm "Ты только это, не уходи там далеко!"

    "Мне не очень нравится, чем это может обернуться, если мама всё-таки еще дома."

    "Но пути назад уже нет. Если что-то пойдёт не так, попробую выкрутиться."

    "Обычно у меня получается это сделать без проблем, но вот с мамой…"

    "Собравшись с мыслями, я открываю дверь."

    scene bg room
    with fade

    "Меня никто не встречает, однако я сразу подмечаю обувь, которой тут быть не должно. Все таки мама дома…"

    m "Дима, это ты?"

    "Чёрт… сразу я, а не отец? Неужели легенды о материнском инстинкте имеют смысл?"

    d blya "Да, мам, это я…"

    m "А ты чего так рано?"

    "Мой мозг начал активно работать, чтобы придумать выход из сложившейся ситуации. После небольшого обдумывания, я нашёл два варианта…"
    stop music fadeout 1
    menu:
        "Как выкрутиться?"

        "Забыл тетрадь":
            jump hata_tetr

        "Отмена уроков":
            jump hata_urok

    return

label final:
    scene bg dvor
    show stas norm
    play music mus_go

    s "Ну что, куда пойдем дальше?"
    extend proud" Хотя стоп, а который час?"

    "Посмотрев в очередной раз на время, можно сделать вывод, что задача практически выполнена."

    "До конца занятий осталось минут 40.{w} В самом разгаре последний урок, прогул которого стал причиной всех приключений."

    d smile "Давай потихоньку расходиться что ли…"

    s smushenie " А, и правда. Время уже почти подошло."

    scene bg dvor
    with fade

    show stas norm

    "Мы двигаемся вдоль нашего переулка, когда на пути встречаем того, кого ожидали увидеть меньше всего."
    stop music fadeout 1
    "Парень в школьной форме подходит к нам…"

    show stas smushenie
    v "Здарова, пацаны! А чего вас сегодня не было?"

    "Мы переглянулись со Стасом. {w}Такого явно не было в наших планах."

    "Наш одноклассник, Валера, стоит перед нами во время контрольной по геометрии."
    play music mus_sad

    "Он не из тех, кто прогуливает школу. Сколько я себя помню, у него не было дней, когда он бы пропустил школу."

    "Даже если он болел, он все равно приходил, несмотря на все требования учителей и одноклассников спокойно отдыхать дома."

    "И вот он. Стоит перед нами, с лёгкой улыбкой смотря на то, как мы пытаемся подобрать слова."

    v "Понятно… Чего, решили прогулять контрольную?"

    d blya "Да, решили прогулять…"

    "Все было спланировано насколько это возможно.{w} Столько приключений мы прошли вместе со Стасяном, чтобы быть пойманными как дети в последний момент."

    v "Прикол хотите? А её сегодня и не было!"

    s angry "Эээ?!{w} Всмысле?!"
    stop music fadeout 1

    "Недоумевая, мы сверлим взглядом гонца, что принес нам такую весть, ожидая узнать продолжение."

    "Слыхал я, что за плохие новости гонцов могли казнить, однако не мог понять, к какому типу относятся такие новости…"
    play music mus_go

    v "Ну да. Оксана Игоревна заболела, поэтому контрольную перенесли на следующую неделю…"

    "Посмотрев друг другу в глаза, единственное, что у нас было на уме, тут же было озвучено."

    s smushenie "Блять…"

    d blya "Блять…"

    "И только Валера, глядя на нашу реакцию, не смог сдержать смех."

    "Жаль, что его смех мы не могли разделить, пускай и наша цель была в каком-то смысле перевыполнена…"

    "Вдоволь насмеявшись, он задал ключевой вопрос."

    v "Ну что, следующую среду тоже будете прогуливать?"

    "..."

    "..."

    scene bg school
    with fade

    "Мы шли вдоль школы, рассказывая Витале приключения, через которые мы сегодня прошли ради того, чтобы пропустить контрольную."

    "Он с интересом слушал наш рассказ посмеиваясь вместе с нами."

    show stas norm

    "Дойдя до рокового переулка, мы думали расходиться."

    v "Я конечно понимаю чего вы так расстроены… {w}Ну, человеку свойственно ошибаться, что тут сказать…"

    "По крайней мере, вы целы, здоровы, ещё и прогулялись просто так."

    "Вы только это, не повторяйте эту клоунаду больше."

    show stas smushenie


    menu:
        ""

        "Сегодня был, конечно, интересный день, но лучше все же пойти на контрольную":
            $fin=True

        "Сегодня был самый ужасный день в моей жизни, лучше уж пойти на контрольную":
            $fin=False

    s angry "Ну его нахер. Уж проще подготовиться к контрольной и спокойно отсидеть эти уроки."

    v "Это правильно. Тем более там будут сокращённые дни, может не так строго будут проверять контрольную…"

    "Мы единогласно сошлись на том, что лучше оставить попытки прогуливать контрольные, особенно по таким предметам."

    "Ну а если и прогуливать, то тщательнее планировать все."
    stop music fadeout 1
    scene bg black
    with fade
    "На этом я вернулся домой и практически сразу завалился спать, переваривая случившееся сегодня."


    if fin:
        
        jump epilog1

    else:

        jump epilog2

    return

label epilog1:
    play music mus_bar
    scene bg bar
    with fade

    stm "Да уж! Были ведь времена, да, Димас?"

    dm "Да, даже как-то жаль, что такого движа больше нет."

    stm "Ага, я бы не прочь вновь с теми окурками повоевать!"

    "Даже спустя десять лет, Стасян остается человеком, который готов к бою."

    "Это хорошо, что после армии он не потерял своего оптимизма."

    "Мне очень не хватало этой энергии, которой он всегда делился со мной."

    "В последние дни, по окончанию универа и старте рабочих будней, очень приятно вспомнить школьные дни и хотя бы как-то встретиться с боевым товарищем, с которым прошел огромный путь."
    
    stm "Как ты думаешь, было ли тогда ошибкой с нашей стороны пытаться прогуливать школу?"

    dm "Возможно… Но даже так, я считаю, что мы все сделали правильно. Пускай мы и не достигли цели, но ведь какая история получилась, правильно?"

    stm "Это точно, есть что вспомнить. Ну что, с тебя кружка темного, ты ведь тогда проиграл, помнишь?"

    dm "Как это проиграл? Я-то все помню прекрасно. Я тебя тогда как ребенка уделал!"

    stm "Ну так давай решим вопрос, как в старые добрые?"

    dm "А погнали…"
    stop music fadeout 1
    scene bg black
    with fade


    return

label epilog2:
	scene bg bar
    with fade
    play music mus_bar
    dm "Да… {w} В тот день я чувствовал себя самым большим на свете неудачником."

    dm "Мне так хотелось, чтобы того дня в моей жизни просто не существовало."

    stm "Кстати, Дим, насчёт того клуба… {w}Он короче закрылся спустя год как мы школу закончили..."

    stm "Продержался считай меньше всех…"

    stm "В общем, я хочу там свою автомастерскую открыть когда-нибудь. {w}Ну, когда с деньгами попрёт нормально."

    dm "Работая механиком-то… сомневаюсь, что ты скоро накопишь на это дело."

    stm "Да не, ты чего, я нормально в общем-то получаю."

    stm "Начальник говорит, что скоро меня повысит… {w}Мы с ним вечно спорим о тачках, он мне втирает что немцы круче американцев… короче мы с ним закорешились неплохо."

    dm "Кстати, а как ты узнал где я сейчас работаю?"

    stm "Мамка твоя мне рассказала."

    stm "Приходила ко мне на работу, рассказывала, как она там."

    stm "Я ей тоже рассказывал о нашей прогулке, уже чуть поподробней. Смеялась. Просила передать кое-чего…"

    dm "А?"

    stm "Ну… как бы так сказать… как-то самому неловко… просила передать что рада, что у тебя был такой друг типа меня."

    dm "Эээ… ого.{w} Это… неожиданно."

    "Мысли сошлись в небольшую кашу."

    "Почему просила передать? Почему не сказала об этом мне напрямую, почему просила передать эти слова через Стасяна?"

    "Как она вообще нашла где он работает?"

    "Впрочем, последний вопрос меня тревожил менее всего."

    "Уже не знаю сколько времени прошло, пока я пытался понять, что происходит. Каша моих мыслей кипела вовсю."

    "И тут… {w}мне стало всё понятно. Получившееся варево… меня приятно удивило."

    "На самом деле, если бы не Стас, я бы ни за что не согласился на нечто подобное, неважно насколько сильно я бы хотел чего-либо избежать, исправить."

    "А Стасян умел настроить на решительные действия. Мне всегда импонировал его боевой характер."

    "Даже сейчас, глядя на него, можно разглядеть этот весёлый огонёк в глазах…"

    "Раньше я жалел, что я в принципе решил свалить, корил себя за то, подставляю себя, своих родителей, своего друга (ещё бы, сам такими вещами он вряд ли занимался бы)…"

    "одним словом, раньше эту оплошность я не мог себе простить."

    "Сейчас же, вернувшись к этой теме… я ощущаю совсем другое."

    "Я ощутил необычайную лёгкость. Словно с моих плеч спал груз…"

    "Возможно, вы помните какие в старых компьютерных клубах мониторы стояли?"

    "Вот примерно четыре таких с меня упало одновременно в тот вечер. И я похоже понимаю почему."

    "Да, я не горжусь совершённым поступком. {w}Я признаю, что мы поступили очень необдуманно."

    "Однако… {w}такая ошибка была неизбежна."

    "Не решись мы на тот поступок, и не случись оно настолько, казалось бы, ужасно… кто знает, каким боком бы вышли наши следующие идеи…"

    "Теперь, то, что ранее я считал самым неудачным днём… {w}на самом деле был самым удачным днём в нашей жизни."

    "Разумеется, школу прогуливать я более не пытался. И после наших злоключений мы со Стасом начали учиться, пусть и ненамного, но всё же лучше."

    "А главное: мы направили нашу энергию в другие, не менее интересные вещи."

    "Я завёл себе дневник, расписал этот день как самый ужасный в своей жизни, приукрасив в некоторых местах."

    "И, признаться, так меня это затянуло… {w}что теперь я, находясь где-нибудь в отъезде, каждый вечер стараюсь часть своих мыслей перенести на бумагу."

    "Кажется, таких людей называют писателями."

    "Стас мне рассказал, что когда мы с ним в тот день носились по гаражам, он заприметил, как из одной выезжала машина."

    "И вот настолько она ему запомнилась, что он, в желании узнать её модель и как её достать, пока двигался к своей цели, узнал ещё о нескольких интересных моделях, узнал, чем отличается их строение, чем отличаются движки…"

    "Отчего он и принял решение что больше всего хочет собрать машину своей мечты, а также копаться в машинах."

    "И вот теперь, после этих причудливых скитаний по нитям судьбы, мы пришли к столику, где сейчас сидим, и где составляем свои выводы."

    "Как я уже говорил, эта ошибка была неизбежна."

    "Ошибка, которую мы должны были совершить, чтобы оказаться здесь, теми, кем сейчас и являемся."

    "Теперь же, когда клубок распутан, можно заняться одним из своих любимых увлечений."
    stop music fadeout 1

    scene bg black
    with fade

    "Я выглянул в окно, чтобы полюбоваться видом, как на небе появляются первые звёздочки."

    "Город медленно начинает уходить в сумерки. Наступает тёплая майская ночь…"

    scene bg black
    with fade

    return

label garage_move:

    d think "Я понимаю, но ты смотри. {w} А если тут охрана есть? {w}Мы уже и так шуму наделали, пока бегали. {w}Мне кажется, сторож уже скоро придёт и нам мало не покажется."

    s angry "Есть такое, но всё же победителя определить нужно…"

    d smile "За это ты не переживай, мы с тобой сегодня ещё выясним, у кого самые быстрые ноги на Диком Западе."

    hide stas

    "Явно расстроившись таким решением, Стасян сбавляет обороты и соглашается."

    " После того, как мы слезли с гаражей, я заметил странное движение около наших сумок."

    " Мои подозрения подтвердились – в этом кооперативе много бродячих собак."

    " И одна из них очень заинтересована в наших вещах."

    " Переглянувшись со Стасом, мы направились за сумками."

    " Собака, увидев нас, испугано убежала."

    d ok "Хм? Да уж, я боялся проблем с ней будет больше…"
    stop music fadeout 1
    "Забрав сумки, мы направились к выходу, однако на пути встала далеко не одна собака, как раньше."
    play music mus_dogez
    "Неизвестно откуда появившись, целая стая собак начала с любопытством двигаться в нашу сторону."

    " Довольно быстро нас окружило мохнатым кольцом, попытки вырваться из которого резко пресекались поднимаемым лаем."

    show stas smushenie

    s smushenie "Да блеа, ненавижу собак. Меня и парочка таких может задолбать по дороге домой, а тут…"

    d wat "Что делать будем?"

    s angry "Хрен его знает. Стоять на месте – хреновая идея. Но и двигаться мы не можем. Готовься с боем вырываться…"

    d blya "Чего? Их тут на весь наш класс по штуке соберётся! Ты уверен..?"

    hide stas

    "Стасян разминает кулаки.{w} Видимо, он настроен серьезно."

    " Я очень боюсь собак, но Я тоже не собираюсь стоять в стороне."

    " Видимо сегодня мне придется конкретно заняться своей формой, и побороться со своими страхами."

    "Как только лай собак начал сливаться в один сплошной поток, мы приготовились к бою…"

    "Внезапно в этой мешанине звуков мы чётко услышали фразу"

    who "Э, съебали отсюда, сукины дети!"

    "В ответ на этот клич собаки резко умолкли и начали бежать куда глаза глядят."

    " Я тоже чувствую, что нам стоит последовать примеру собак и так же рвануть, ведь голос принадлежал…"
    stop music fadeout 1
    show stas happy
    play music mus_danger
    s happy "Сторож! Охренеть! Мы спасены!"

    d think "Ты уверен? Мы же тут круги наматывали, думаешь нам не влетит?"

    s smushenie "Блед, ты прав…"

    d norm "Ладно уж, попробуем выкрутиться… Просто скажи, что…"

    "Договорить я правда не успел… Чёрт, надеюсь, он будет молчать как рыба…"

    "К этому моменту сторож уже подошел на достаточно близкое расстояние, однако мы слишком напуганы, чтобы рассмотреть нашего спасителя."

    show storozh at left

    st norm "А вы тут что забыли? Разве у вас уроки не начались еще?"

    d ok "Нам ко второму, как раз вот идём. Ну что, Стас, погнали?"

    s happy "С-спасибо, что помогли нам!"

    "Что-то он совсем лицо потерял… не похоже это обычно на него."

    st "Вот как… Боюсь, пацаны, вы немного опаздываете для второго урока… {w}впрочем ладно, вы я смотрю и так напуганы, вон как у вас дыхание перехватывает…"

    st "Не моё это дело, одним словом. Задам последний вопрос. Не бегали по гаражам?"

    d wat "Не, мы только мимо проходили, почему вы решили, что мы могли этим заниматься?"

    st "Обычно они сбегаются, когда какие-то дебилы начинают гонки устраивать по гаражам."

    st "Это хорошо, что я услышал собак.{w} Так бы мог не успеть к вам."

    st "Был тут недавно случай, когда покусали пару пацанов конкретно так..."

    st "Давайте, не опаздывайте. {w}И лучше идите по дороге, чтобы снова на них не наткнуться."

    "Хором поблагодарив старика и судьбу мы побежали подальше от этого места."
    stop music fadeout 1

    scene bg dvor
    with fade

    st "Ну что, что будем делать дальше?"

    jump choise

    return

label garage_stay:
    d smile "Ладно, раз не уйдешь, то и оставайся тут до конца дня! {w} Я не отдам тебе победу так просто."
    play music mus_run
    scene bg garage
    with fade

    "Собрав волю в кулак мы готовимся к финальному рывку."

    "Со всей силы я стартую, мы со Стасяном идем ноздря к ноздре."

    "Все настроены серьезно, никто не готов уступать первое место."

    "На протяжении финального забега, дистанция между нами не меняется."

    "Мы бежим как единое целое.{w} Глаза сверкают, легкие горят, в ногах приятная боль."

    "Мы рвемся к победе как львы к добыче. {w}И вот, приближаясь к финишной черте мы слышим сигнал о завершении забега."

    "Однако это не привычный нам сигнал, поскольку мы слышим вдалеке незнакомый голос и фразу…"
    stop music fadeout 1

    who "Э, дети ёбанные, а ну съебались отсюда быстро!"

    "Несмотря на то, что источник явно находился далеко от нас, этот голос явно дал нам понять, что пора уносить ноги."

    "Переглянувшись со Стасяном, мы поняли друг друга без слов."

    "Вот-вот тут будет сторож, от которого нам достанется, если поймает."

    "Ключевое тут{w} – если поймает."
    play music mus_run

    scene run
    with fade

    "Как только мы пересекли финиш, забег продолжился, с неплохой такой импровизацией."

    "Мы направились к гаражу, через который мы попали на нашу беговую дорожку."

    "Со скоростью выпущенной стрелы мы добрались до цели и, недолго думая, спрыгнули вниз за сумками."

    "Как только мы подобрали наши вещи, недалеко от входа мы заметили движение."

    "Недолго думая, мы продолжили наше соревнование, главным призом в котором стало не просто «первое место», но сохранность наших жизней."

    "Добежав до знакомых улиц, мы без сил упали на лавочку около ближайшего подъезда."

    "Сколько мы бежали – никто не знает. {w} Однако топота ног я больше не слышу. {w}Это вероятней всего значит, что сейчас находимся в безопасности."

    scene bg dvor
    with fade
    stop music fadeout 1
    "В этой гонке каждый из нас вышел победителем, что не могло не радовать."

    show stas
    play music mus_stas
    s smushenie "Уф… {w}Ну что… {w}кто победил?"

    d ok "Даже не знаю… {w}Мне кажется… {w}можно сойтись на ничье…"

    s angry "Опять?!{w} Да за кого ты меня держишь… {w}Я тебя как ребёнка размотал…"

    d blya "Ты хочешь у сторожа спросить кто победил?"

    s smushenie "Не, ну его нахер… {w}Интересно, а бегуны так же на рекорды тренируются…"

    hide stas

    "Мы сошлись на том, что победителя нашего Гаражного Гран-При определит следующий забег, но это будет уже в другой раз."

    "Спортсмены не в состоянии продолжать борьбу."

    "Переведя дух перед нами вновь встал насущный вопрос."

    "Что делать дальше? {w} Посмотрев на часы, мы поняли, что до конца уроков еще далеко."

    d think "Ну что, чем займемся дальше?"

    show stas

    s smushenie "Хрен его знает, у меня абсолютно нет идей…"

    "Ладно, какие еще у нас были варианты?"
    stop music fadeout 1
    jump choise

    return


label komp_agree:
    play sound sound_club
    d ok "Ладно, надоел. Садись."

    "Довольно потирая руки, на которых живого места практически нет, гопник садится поудобнее и присоединяется к битве."

    hide gopnik

    "Поскольку мне тут делать более нечего, я встаю рядом со Стасом и смотрю, что происходит у него"

    show stas proud

    s "О, Димас. А ты тут чего?"

    "Только посмотрев в сторону моего места, ему стало все понятно. С несколько разочарованным взглядом он поворачивается ко мне."

    s norm "Что делать будешь?"

    d ok "Да уже ничего. {w} Место мне, понятное дело, не вернут. Так что проще уже забить."

    s angry "Уверен? Я бы ему пояснил, что культурней себя стоит вести в обществе."

    "Стасян разминает кулаки. Не знаю, файтингов мне и на компе хватает..."

    d norm "Да, уверен. Забей. Сколько по времени осталось?"

    s norm "Да хрен его… может, Минут 30 еще. А что?"

    d ok "Я вот думаю, может мне пока до магазина сходить? Там куплю нам чего…"

    s happy "Погнали вместе. Мне-то тут тоже делать нечего, раз у тебя место отжать успели."

    d smile "Ну, можно и так. Спасибо. Давай тогда глянем, что там есть интересного."

    hide stas

    "Стасян незамедлительно встал со своего места, и мы направились к выходу."

    "Перед тем как покинуть клуб, мы бросили взгляд на потерянные компы, на которых вовсю резвились самые боевые из отряда гопников, заливая зал своим смехом."

    "Получилось конечно очень неприятно, однако кто знает, чем могло это закончиться."

    stop sound fadeout 1

    scene bg komp
    with fade
    play music mus_walk
    "Обсудив дальнейший план, мы направились в магазин, чтобы хоть как-то скомпенсировать горечь от сложившейся ситуации."

    "Перекус, к моему удивлению, очень недурно поднял мне настроение."

    scene bg dvor
    with fade

    show stas happy

    "Стас тоже улыбается во всю ширь… {w}Что ж, в таком случае, день не испорчен окончательно, и мы можем спокойно решить что делаем дальше…"

    s "Ну что, куда пойдем дальше?"
    stop music fadeout 1
    jump choise

    return

label komp_refuse:
    play sound sound_club
    d norm "Иди тряси кого-то другого, мне надо ему кабину начесать."

    gp angry "А что если Я тебе кабину начешу, шкет?"

    d ok "Как-нибудь в другой раз..."

    "Моё плечо почувствовало свободу, а так же небольшой поток воздуха."

    "Недалеко от моей головы пролетел кулак, явно видевший не одну драку."

    hide gopnik

    "Попытка запугать не удалась, из-за чего ярко выразив свое недовольство, гопник уходит вглубь клуба, как мне показалось, трясти других."

    "Небольшая победа в этом разговоре меня порадовала, осталось только победить самого главного противника – Стасяна."

    scene bg club
    with fade

    "Через полчаса у нас кончается время."

    show stas norm

    "Мне удалось обыграть Стаса, однако он не сильно расстроен."

    "Все же время пролетело незаметно и очень увлекательно."

    "Собираясь двигаться на выход он задает интересный вопрос."

    s happy "Димас, а что за хер около тебя стоял, пока мы играли?"

    d ok "Очередной гопник, желающий «поиграть на пять минут»."

    s "А, вот оно что. Ну так это, красава, что отмахнулся от него."

    d "Я сам удивился, что он так легко сдался…"
    extend norm" Хотя у меня есть подозрение, что на этом всё не закончилось. {w}Так что будь готов."

    s smushenie "К чему?"
    stop sound fadeout 1
    "Я указываю взглядом на выход. Там уже собралась толпа гопников, во главе с моим недавним собеседником."

    "Ухмыляясь, они ожидают, когда мы выйдем."
    play music mus_danger
    "Надеяться на помощь со стороны клуба не приходится."

    "Всё, что происходит снаружи, их не касается."

    "Долго оставаться тут мы тоже не можем, поэтому надо решаться."

    "Собрав волю в кулак, я направился к выходу. Стас не отстает."

    scene bg komp
    with fade

    show gopnik happy at left

    "Мы попали в кольцо из спортивных курток, в которой Стасян смотрится на удивление органично."

    show stas angry at right

    "Мои размышления о сложившейся ситуации прерываются, когда главарь открывает рот."
    
    gp norm "Пацаны, нам бы тут это, поиграть хотелось. {w}А денег немного не водится, подкиньте, а?"

    show gopnik happy

    "Взявшие нас в кольцо гопники начали ехидно улыбаться, показывая нам, что деваться некуда."

    "Схема отработанная, такую картину можно довольно часто наблюдать тут."

    "Удивительно конечно, что даже утром они обитают тут."

    "Все же по утрам тут мало людей, но видимо нам очень сильно повезло."

    "Я придумал несколько вариантов, как нам выкрутиться из сложившейся ситуации."

    "Либо просто отдать деньги и спокойно пойти, не тратя на них свое время, либо все же попытаться дать отпор."

    "Конечно, шансы невелики, но со мной Стасян, который и не в таких ситуациях бывал."

    show stas norm
    stop music fadeout 1
    "Он смотрит на меня, ожидая дальнейших действий."

    menu:
        "Как действовать дальше?"

        "Отдать деньги":
            jump komp_pay

        "Защищаться":
            jump komp_fight

    return

label komp_pay:
    play music mus_ble
    d ok "Ладно, заебёте"

    show gopnik happy

    "Я выдаю последние монеты, что хранились в моем кармане."

    "Я чувствую, что это неправильно, но иного пути нет."

    "Если мы хотим выйти отсюда целыми и не вызывать подозрения у родных после, то нужно попытаться замять эту ситуацию."

    show stas smushenie

    "Стас, очень недовольный этим решением, всё же смиряется, вспоминая то, что мы прогуливаем школу, а не пришли сюда просто развлечься."

    gp "Хах, надо было сразу тебя прочесать."
    extend norm "Мужики, идем брать места, пока какие-нибудь еще ебланы не заняли наши места."

    gp angry "Пиздуйте пока целые"

    hide gopnik

    "Напоследок главарь дает мне пинка, после чего растворяется в клубе. Остальные из его группы следуют за ним."

    s angry "Да уж, ненавижу таких типов. Ты там как, цел?"

    d blya "Да, жить можно. Я хотел после этого заглянуть в магазин, но видимо не выйдет."

    s "Ладно уж, легко отделались. Пойдём тогда дальше?"

    d ok "Погнали."

    "Оттряхивая место удара, мы идем подальше от этого места, чтобы не попасть в лапы к очередной группе."
    stop music fadeout 1
    scene bg dvor
    with fade

    show stas norm

    s "Ну что, куда двинем дальше?"

    jump choise

    return

label komp_fight:
    play music mus_fight
    show stas angry
    "Я киваю Стасу, после чего сжимаю кулаки."

    show gopnik happy

    "Увидев нашу реакцию, у главного гопника улыбка растянулась еще больше. Кажется, его лицо готово треснуть еще больше."

    s "Хрен вам, а не деньги."

    "Стасян летит вперед."
    show gopnik angry
    "Однако главарь довольно легко уклоняется от атаки и контратакует Стаса."

    "Рефлекторно тот поставил блок, из-за чего удар приходится в руку, а не в голову."

    "Не успев среагировать, стоящие сзади гопники хватают меня, пресекая любые мои попытки вырваться."

    "Только главарь замахивается на Стаса для нанесения следующего удара, по улице разносится оглушительный свист."
    
    hide stas

    show storozh norm at right

    who "Серый, ты не охуел? Я же тебе говорил, что по средам это мое место!"

    "Бросив взгляд в строну источника этой фразы, я увидел другого гопника со своей командой."

    hide storozh
    show gop norm at right
    show gopnik norm at left
    "Державшие меня охранники отпустили меня и перегруппировались, чтобы быть ближе к своему лидеру."

    gp angry "Всмысле? {w}Ты же тут в понедельник трешься? {w}Так какого хера это твое место сегодня?"

    show gop rage

    who "Ты не понял? Сваливай отсюда, и псов своих забирай, пока не отлетел."
    scene gop
    with fade

    "Видимо у них начались внутренние разборки."

    "Они стоят стенка на стенку, ожидая команды для атаки от своих лидеров."

    "Ни один из этих стервятников не готов уступать свою территорию."
    stop music fadeout 1
    "Пока происходят эти разборки, я даю команду Стасяну валить отсюда, пока не поздно."

    "В таких ситуациях минус на минус даёт плюс, и пользоваться нужно прямо сейчас."
    play music mus_run
    "Мы бежали как могли."

    "Не слыша ничего, кроме ругани гопоты мы добрались до безопасного места."

    scene bg dvor
    with fade
    stop music fadeout 1
    show stas smushenie

    d blya "Ты как, цел? Не сильно досталось?"

    s angry "Кулак у него железный, конечно. Рука еще неделю будет болеть..."

    s norm "В целом пойдёт, ты как?"

    d ok "У меня всё норм. Шея только натирает… {w}Меня особо не задело никак, считай повезло."

    s "Это верно. Давай-ка больше не будем сюда ходить?"

    d norm "Согласен, мне сегодня такого точно хватило…"

    s happy "Ладно уж, что дальше по плану?"

    jump choise

    return


label hata_tetr:
    play music mus_ble
    d ok "Да я забыл тетрадь по геометрии. {w}Ты же знаешь, Оксана Игоревна может поставить на уши весь класс, если кто-то забывает ДЗ."

    m "Ох уж эта Оксана Игоревна... Сколько раз на нее другие родители на собрании жаловались."

    m "Ладно, давай быстрее, пока не опоздал на уроки! Не хочу, чтобы моего мальчика окрестили прогульщикам, хихи!"

    "После этих слов я почувствовал себя весьма неудобно…"

    "Я всегда старался не огорчать маму лишний раз, а в такой-то ответственный момент… {w}если вдруг правда откроется…"

    "Мама, прости меня, это только на один раз."

    "Выдохнув, я побежал в свою комнату."

    scene bg room
    with fade

    "Я был удивлен, что мне так просто получилось выкрутиться из такой непростой ситуации, однако радоваться я буду только после того, как вся сегодняшняя операция будет успешна."

    "А сейчас мне нужно сделать вид, что я активно ищу потерявшуюся тетрадь."

    "После всех приготовлений, я обулся и попрощался с мамой."

    "Напоследок она, как обычно, невозмутимо, словно я спешу на первый урок, попросила меня учиться хорошо и приносить хорошие оценки домой."
    stop music fadeout 1
    scene bg dvor
    with fade
    show stas smushenie
    "Выбежав из подъезда, я встретился взглядом со Стасом."

    "Видимо на моем лице всё было написано, отчего тот, с погрустенвшим лицом, молча кивнул, и мы пошли в сторону нашего переулка."

    scene bg dvor
    with fade
    show stas norm
    s norm "Ну что, как тогда действуем дальше?"

    jump choise

    return

label hata_urok:
    play music mus_ble
    d blya "Да у нас сегодня уроки отменили. {w}Учительница заболела, а у нас было от неё аж 3 урока.{w} Вот я и пришел раньше."

    m "Как-то с трудом верится, что вас бы так легко отпустили.{w} Покажешь дневник?"

    "У меня, конечно же, дневник не заполнен."

    "Но даже так, информации, которую она хочет там увидеть, нет и не было. Нужно попытаться выкрутиться из ловушки, в которую я себя загнал."

    m "Дима, я жду…"

    d ok "Мам, я дневник оставил в школе у классного руководителя.{w} Вроде как будут проставлять информацию для родителей."

    m "А, точно, надо бы позвонить Оксане Игоревне, чтобы обсудить детали по поводу денег за шторы в классе."

    d wat "А давай я передам? Я так подумал, лучше будет, если я сейчас заберу свой дневник.{w} Заодно уточню насчет штор…"

    m "Ну… Так тоже можно, я думаю."

    d smile "Отлично, я тогда пойду"

    m "Хорошо, давай. Надеюсь, что она ещё на месте."

    "Я спешно закрываю дверь снаружи."

    "Глубокий выдох…{w} Получилось… {w}очень ловко получилось."
    stop music fadeout 1
    scene bg dvor
    with fade

    show stas smushenie

    "Выбежав из подъезда, я встретился взглядом со Стасом."

    "Видимо на моем лице все было написано, отчего тот поник взглядом, однако молча кивнул, и мы пошли в сторону нашего переулка."

    scene bg dvor
    with fade

    show stas norm

    s "Что у нас еще по рассписанию?"

    jump choise

    return
